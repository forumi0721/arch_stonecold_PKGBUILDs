# Maintainer: StoneCold <forumi0721[at]gmail[dot]com>

_pkgbase="distccd-alarm"
pkgname=("${_pkgbase}-armv5" "${_pkgbase}-armv6h" "${_pkgbase}-armv7h")
pkgver=5.3.0
pkgrev=4
pkgrel=1
pkgdesc="An x-tools & distcc services package for Arch Linux ARM"
arch=("x86_64")
url="http://archlinuxarm.org/developers/distcc-cross-compiling"
license=("GPL" )
depends=("distcc")
options=("libtool" "emptydirs" "!strip")
source=(
	"alarm-x-tools5.tar.xz::http://archlinuxarm.org/builder/xtools/${pkgver}-${pkgrev}/x-tools.tar.xz"
	"alarm-x-tools6.tar.xz::http://archlinuxarm.org/builder/xtools/${pkgver}-${pkgrev}/x-tools6h.tar.xz"
	"alarm-x-tools7.tar.xz::http://archlinuxarm.org/builder/xtools/${pkgver}-${pkgrev}/x-tools7h.tar.xz"
	"alarm-x-tools8.tar.xz::http://archlinuxarm.org/builder/xtools/${pkgver}-${pkgrev}/x-tools8.tar.xz"
	"${_pkgbase}.tar.xz"
)
md5sums=(
	"e6ed76ee68876ad0a88c1959c7a1894b"
	"80ab8403a843da1cf1c51b9ee3f8b634"
	"e722e2e93af5c62fab5251f26fdaf3b8"
	"336a26f915893a49c66fc7e30d1cc60b"
	"defa43e9e1f2ed3734e3ceeffc77f6a9"
)

_package_subarch() {
	local _arch="${1}"
	local _srcdir="${2}"
	local _pkgdir="${3}"

    # install symlink to distccd
    install -dm755 "${pkgdir}/usr/bin"
    ln -sf /usr/bin/distccd "${pkgdir}/usr/bin/distccd-${_arch}"

    # copy in toolchain
    install -dm755 "${_pkgdir}"
    cp -ar "${_srcdir}" "${_pkgdir}"

    # install services
    install -Dm644 "${srcdir}/distccd-${_arch}.service" "${pkgdir}/usr/lib/systemd/system/distccd-${_arch}.service"

    # install config
    install -Dm644 "${srcdir}/distccd-${_arch}.conf" "${pkgdir}/etc/conf.d/distccd-${_arch}"
}

package_distccd-alarm-armv5()
{
	install="${pkgname}.install"
    backup=("etc/conf.d/distccd-armv5")
	local _arch="armv5"
	local _srcdir="${srcdir}/x-tools"
	local _pkgdir="${pkgdir}/usr/local/x-tools-armv5"
	_package_subarch "${_arch}" "${_srcdir}" "${_pkgdir}"
}

package_distccd-alarm-armv6h()
{
	install="${pkgname}.install"
    backup=("etc/conf.d/distccd-armv6h")
	local _arch="armv6h"
	local _srcdir="${srcdir}/x-tools6h"
	local _pkgdir="${pkgdir}/usr/local/x-tools-armv6h"
	_package_subarch "${_arch}" "${_srcdir}" "${_pkgdir}"
}

package_distccd-alarm-armv7h()
{
	install="${pkgname}.install"
    backup=("etc/conf.d/distccd-armv7h")
	local _arch="armv7h"
	local _srcdir="${srcdir}/x-tools7h"
	local _pkgdir="${pkgdir}/usr/local/x-tools-armv7h"
	_package_subarch "${_arch}" "${_srcdir}" "${_pkgdir}"
}

package_distccd-alarm-armv8()
{
	install="${pkgname}.install"
    backup=("etc/conf.d/distccd-armv8")
	local _arch="armv8"
	local _srcdir="${srcdir}/x-tools8"
	local _pkgdir="${pkgdir}/usr/local/x-tools-armv8"
	_package_subarch "${_arch}" "${_srcdir}" "${_pkgdir}"
}
